
def getToolchainFile() {
    if (project.hasProperty('TOOLCHAIN')) {
        return "-DCMAKE_TOOLCHAIN_FILE=${rootProject.file(project.property('TOOLCHAIN'))}"
    }
    return null
}

def getAdditionalProperties() {
    if (project.ext.has('CMAKE_PROPERTIES')) {
        return (String) project.ext.get('CMAKE_PROPERTIES')
    }
    return null
}

def BUILD_DIR = new File(project.buildDir, 'cmake')
def CMAKE_FILE = project.file('native/CMakeLists.txt')

task deleteBuildDirectory(type: Delete) {
    delete BUILD_DIR.absolutePath
}

task createBuildDirectory(type: Exec, dependsOn: deleteBuildDirectory) {
    commandLine 'mkdir', '-p', BUILD_DIR.absolutePath
}

task runCmake(type: Exec, dependsOn: createBuildDirectory) {
    workingDir BUILD_DIR.absolutePath

    def cmd = ['cmake']

    def toolchainFile = getToolchainFile()
    if (toolchainFile != null) {
        cmd.add toolchainFile
    }

    def additionalProperties = getAdditionalProperties()
    if (additionalProperties != null) {
        cmd.add additionalProperties
    }

    cmd.add CMAKE_FILE.parentFile.absolutePath

    commandLine cmd
}

task runMake(type: Exec, dependsOn: runCmake) {
    workingDir BUILD_DIR.absolutePath
    commandLine 'make'
}

task buildNatives {
    dependsOn tasks.runMake
}
