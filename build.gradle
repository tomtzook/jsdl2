plugins {
    id 'java-library'
}

apply from: rootProject.file('extras.gradle')

group GROUP
version VERSION

ext.FLAVORS = [
        'linux-x86_64': [
                'toolchain': null,
                'output': 'libsdl2_jni.so'
        ],
        'windows-x86_64': [
                'toolchain': 'platforms/x86_64-mingw-gcc.cmake',
                'output': 'libsdl2_jni.dll'
        ]
]

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.4.2'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.2.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.23.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.1'
}

test {
    useJUnitPlatform()

    testLogging {
        events "failed"
        exceptionFormat "full"
        showStackTraces true
    }
}

jar {
    archiveBaseName.set(project.name)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveBaseName.set(project.name)
    archiveClassifier.set('sources')

    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName.set(project.name)
    archiveClassifier.set('javadoc')

    from javadoc.destinationDir
}

task jsdl2All(type: Jar, dependsOn: tasks.assemble) {
    dependsOn sourcesJar
    dependsOn javadocJar

    archiveFileName.set('jsdl2-all.jar')
    destinationDirectory.set(project.buildDir)

    from sourceSets.main.output
    from sourceSets.main.allSource
    from javadoc.destinationDir
}

apply from: rootProject.file('cmake.gradle')

tasks.clean.dependsOn tasks.cleanNatives
tasks.jar.dependsOn tasks.buildNatives
